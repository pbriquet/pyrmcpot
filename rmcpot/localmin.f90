! Implement methods that drive the parameter set towards a local minimum
module localmin
	use globals
	use potfunc
	use datafunc

	public		:: minimize
	private		:: stepmin

contains
	! This subroutine calls the appropriate minimization algorithm
	subroutine minimize(minalgo,potmodel,potfile,runpot,maxitermin,convthr,alphamin)
		character*10,intent(in)		:: minalgo
		character*20,intent(in)		:: potmodel
		character*40,intent(in)		:: potfile,runpot
		integer,intent(in)		:: maxitermin
		real,intent(in)			:: convthr,alphamin

		select case(minalgo)
			case('stepmin')
				call stepmin(potmodel,potfile,runpot,maxitermin,convthr,alphamin)
			case default
				stop 'MINIMIZE: Minimization algorithm not supported yet!'
		end select
	end subroutine

	! This subroutine implements a simple (and not very efficient) step-by-step down hill optimization method
	subroutine stepmin(potmodel,potfile,runpot,maxitermin,convthr,alphamin)
		implicit none

		character*20,intent(in)		:: potmodel
		character*40,intent(in)		:: potfile,runpot
		integer,intent(in)		:: maxitermin
		real,intent(in)			:: convthr,alphamin
		integer				:: nfailed=0
		integer,parameter		:: MAXFAILED=4
		double precision,allocatable	:: potdata0(:)
		real				:: stepsign
		real,allocatable		:: defsign(:),alpha(:)
		integer				:: i,j,nacc
		integer				:: genpotstatus=0,getpotdatastatus=0,savebest=0,shellstatus=0
		double precision		:: stepsize,currchi,dchi
		logical				:: next,improved
		double precision,parameter	:: MINSTEPSIZE=1.0d-4
		logical				:: getout=.false.

		allocate(potdata0(ndata))
		allocate(alpha(nparams))
		allocate(defsign(nparams))

		params=bestparams
		potdata0=bestpotdata
		alpha=alphamin
		defsign=1.0

		open(88,file='min_chiconv.dat')

		write(88,'(A)') '# Step Chi Zeta'
		write(88,'(A)') '# ----------------------------------------'
		write(88,*) 0,minchi-minzeta,minzeta

		! Loop over the prescribed minimization steps
		do i=1,maxitermin
			nacc=0

			write(6,'(A,I10)') '=> ITERATION:',i
			
			! Loop over the potential parameters
			do j=1,nparams
				if(fixpar(j)) cycle	! This parameter is fixed; jump to the next one

				next=.false.
				improved=.false.
				stepsign=defsign(j)
				stepsize=abs(params(j)*alpha(j))

				if(stepsize<MINSTEPSIZE) stepsize=MINSTEPSIZE

				prevparam=params(j)
737				params(j)=params(j)+stepsign*stepsize

				! Obtain the current chi
				zeta=0.0d0
				genpotstatus=genpot(potmodel,potfile)

				if(genpotstatus==-1) stop 'STEPMIN: Bad potential parameters!'
				
				shellstatus=system(runpot)	! Run trial simulations with the potential

				if(shellstatus==0) then
					getpotdatastatus=getpotdata()

					if(getpotdatastatus==-1) stop 'STEPMIN: Bad data generated by the potential!'

					call checkdata
	 
					currchi=fchi()+zeta
					dchi=currchi-minchi

					if(dchi<0.0)then
						minchi=currchi
						minzeta=zeta
						potdata0=potdata
						bestparams=params
						savebest=savebestpot(potmodel)
						nacc=nacc+1
						defsign(j)=stepsign
						improved=.true.
						next=.true.

						call saveparamset(minchi,.false.)
						call saveparamset(minchi,.true.)

						if(abs(dchi)<=convthr)then
							write(6,'(A)') '   Desired level of convergence was reached.'

							getout=.true.

							exit
						end if
					else
						potdata=potdata0
						params(j)=prevparam
					end if
				else
					stop 'STEPMIN: Error running the external program!'
				end if

				if(.not.next)then
					stepsign=-1.0*stepsign
					next=.true.
							
					goto 737	
				end if

				if(.not.improved) alpha(j)=alpha(j)*0.5	
			end do

			write(6,*) '=> Number of successful parameter changes:',nacc

			write(88,*) i,minchi-minzeta,minzeta

			if(nacc>0)then	! At least one of the moves improved the objective function
				if(getout) exit

				nfailed=0
			else		! What if no move improves the objective function
				write(6,'(A)') '   STEPMIN: Objective function was not improved in the last iteration...'

				nfailed=nfailed+1
				
				if(nfailed>MAXFAILED) exit
			end if
		end do

		close(88)

		deallocate(potdata0)
		deallocate(alpha)
		deallocate(defsign)

		write(6,*) 'STEPMIN: Local minimization finished!'
		write(6,*)
	end subroutine
end module

